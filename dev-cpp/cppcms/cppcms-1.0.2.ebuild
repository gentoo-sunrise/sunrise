# Copyright 1999-2012 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: $

EAPI=4

PYTHON_DEPEND="2"

inherit cmake-utils multilib python

DESCRIPTION="A Free C++ Web Development Framework aimed for Rapid Web Application Development"
HOMEPAGE="http://cppcms.sourceforge.net/wikipp/en/page/main"
SRC_URI="mirror://sourceforge/${PN}/${P}.tar.bz2"

LICENSE="ALADDIN Boost-1.0 LGPL-3 MIT public-domain"
#This project has LGPL-3 license, but some parts of it are distributed under other mentioned licenses,
#for more information see THIRD_PARTY_SOFTWARE.TXT in docs directory
SLOT="0"
KEYWORDS="~amd64 ~x86"
IUSE="+cache doc examples +fcgi gcrypt +http icu +prefork_cache +scgi +tcpcache"

REQUIRED_USE="prefork_cache? ( cache )
	tcpcache? ( cache )"

DEPEND="dev-libs/libpcre
	sys-libs/zlib
	gcrypt? ( dev-libs/libgcrypt )
	!gcrypt? ( dev-libs/openssl:0 )
	icu? ( dev-libs/icu )
	!icu? ( virtual/libiconv )
"
RDEPEND="${DEPEND}"

src_prepare() {
	python_convert_shebangs 2 bin/cppcms_tmpl_cc
}

src_configure() {
	local mycmakeargs="-DDISABLE_STATIC=OFF
		-DDISABLE_SHARED=OFF
		-DDISABLE_GZIP=OFF
		-DDISABLE_ICU_LOCALE=OFF
		$(cmake-utils_use_disable !icu ICONV)
		$(cmake-utils_use_disable gcrypt GCRYPT)
		$(cmake-utils_use_disable !gcrypt OPENSSL)
		$(cmake-utils_use_disable fcgi FCGI)
		$(cmake-utils_use_disable scgi SCGI)
		$(cmake-utils_use_disable http HTTP)
		$(cmake-utils_use_disable prefork_cache PREFORK_CACHE)
		$(cmake-utils_use_disable tcpcache TCPCACHE)
		$(cmake-utils_use_disable cache CACHE)
		-DLIBDIR=$(get_libdir)"
	cmake-utils_src_configure
}

src_install() {
	cmake-utils_src_install
	dodoc THIRD_PARTY_SOFTWARE.TXT
	if use doc; then
		dohtml -r doc/doxygen/html/*
	fi
	if use examples; then
		rm examples/{autogenerated.doxy,examples.doxy,examples.txt,format.sh} || die
		insinto /usr/share/doc/${PF}
		doins -r examples
	fi
}
